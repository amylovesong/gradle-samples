// buildscript 必须在 plugins 前面
buildscript {
    repositories {
        maven {
            url = uri("file://$projectDir/local-maven/")
        }
    }
    dependencies {
        classpath 'com.amylovesong.s:gradle-samples:1.0-SNAPSHOT'
    }
}

plugins {
    id 'groovy'
    id 'maven'
}

group 'com.amylovesong.s'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
//    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    implementation gradleApi()
    implementation localGroovy()
}

uploadArchives {
    repositories.mavenDeployer {
        snapshotRepository(url: "file://$projectDir/local-maven/")
    }
}

//-------- Custom tasks --------

task greeting(type: org.gradle.GreetingTask) {
    greeting = 'hello from sxl~'
}

// 增量
task incrementalReverse(type: org.gradle.IncrementalReverseTask) {
    inputDir = file('inputs')
    outputDir = file("$buildDir/outputs")
    inputProperty = project.properties['taskInputProperty'] ?: 'original'
}

task updateInputs() {
    doLast {
        file('inputs/1.txt').text = 'Changed content for existing file 1.'
        file('inputs/4.txt').text = 'Content for new file 4.'
    }
}

task removeInputs() {
    doLast {
        file('inputs/3.txt').delete()
    }
}

task removeOutput() {
    doLast {
        file("$buildDir/outputs/1.txt").delete()
    }
}

// 命令行参数
task verifyUrl(type: org.gradle.UrlVerify)

// 命令行参数的取值列表
task processUrl(type: org.gradle.UrlProcess)

//-------- Custom plugins --------
class GreetingPluginExtension {
    String message = 'Hello from GreetingPlugin'
    String greeter
}

class GreetingPlugin implements Plugin<Project> {
    @Override
    void apply(Project project) {
        // Add the 'greeting' extension object
        def extension = project.extensions.create('greeting', GreetingPluginExtension)
        // Add a task that uses configuration from the extension object
        project.task('hello') {
            doLast {
//                println 'Hello from the GreetingPlugin'
//                println extension.message
                println "${extension.message} from ${extension.greeter}"
            }
        }
    }
}

apply plugin: GreetingPlugin

// Configure the extension
//greeting.message = 'Hi from Gradle'
greeting {
    message = 'Hi'
    greeter = 'Gradle'
}

// Working with files
task greet(type: org.gradle.GreetingToFileTask) {
    // lazy evaluation
    destination = { project.greetingFile }
}

task sayGreeting(dependsOn: greet) {
    doLast {
        println file(greetingFile).text
    }
}

ext.greetingFile = "$buildDir/hello.txt"